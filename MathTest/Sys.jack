// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while (true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int l0;
        if (duration < 0) {
            do Sys.error(1);
        }

        while (duration > 0) {
            let l0 = 50;
            while (l0 > 0) {
                let l0 = l0 - 1;
            }

            let duration = duration - 1;
        }

        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        var String s;

        let s = String.new(3);
        let s = s.appendChar(69);
        let s = s.appendChar(82);
        let s = s.appendChar(82);
        do Output.printString(s);

        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}
